$PBExportHeader$populate_ddlb_from_db.srf
$PBExportComments$Fill a DDLB from database using passed table & column
global type populate_ddlb_from_db from function_object
end type

forward prototypes
global function int populate_ddlb_from_db (string tab_parm, string col_parm, dropdownlistbox ddlb_parm)
end prototypes

global function int populate_ddlb_from_db (string tab_parm, string col_parm, dropdownlistbox ddlb_parm);string add_string, select_string

// Set up select statement using passed parameters
select_string = "SELECT DISTINCT " + col_parm + " FROM " + tab_parm
PREPARE sqlsa FROM :select_string;

DECLARE dyn_cursor DYNAMIC CURSOR FOR sqlsa;

OPEN DYNAMIC dyn_cursor;  
if sqlca.sqlcode < 0 then
	MessageBox("DataBase Error!", sqlca.sqlerrtext)
	return sqlca.sqlcode
end if 

ddlb_parm.SetRedraw(false)
ddlb_parm.Reset( )

Do While sqlca.sqlcode = 0
	Fetch dyn_cursor into :add_string;
	if sqlca.sqlcode = 0 then
		ddlb_parm.AddItem(add_string)
	elseif sqlca.sqlcode < 0 then
		MessageBox("DataBase Error", sqlca.sqlerrtext)
		return sqlca.sqlcode
	else
		exit
	end if
Loop

ddlb_parm.SetRedraw(true)
Close dyn_cursor;
return 0
end function

