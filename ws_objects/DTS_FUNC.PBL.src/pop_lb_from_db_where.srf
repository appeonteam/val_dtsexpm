$PBExportHeader$pop_lb_from_db_where.srf
global type pop_lb_from_db_where from function_object
end type

forward prototypes
global function integer pop_lb_from_db_where (string col_parm, string tab_parm, string where_parm, listbox lb_parm)
end prototypes

global function integer pop_lb_from_db_where (string col_parm, string tab_parm, string where_parm, listbox lb_parm);string add_string, select_string
setpointer(Hourglass!)
// Set up select statement using passed parameters
select_string = "SELECT DISTINCT " + col_parm + " FROM " + tab_parm

// If WHERE clause passed
If Len( Trim( where_parm ) ) > 0 Then
	select_string = select_string + " WHERE " + where_parm
End If

PREPARE sqlsa FROM :select_string;
PREPARE sqlsa FROM :select_string;

DECLARE dyn_cursor DYNAMIC CURSOR FOR sqlsa;

OPEN DYNAMIC dyn_cursor;  
if sqlca.sqlcode < 0 then
	MessageBox("DataBase Error!", sqlca.sqlerrtext)
	return sqlca.sqlcode
end if 

lb_parm.SetRedraw(false)
lb_parm.Reset( )

Do While sqlca.sqlcode = 0
	Fetch dyn_cursor into :add_string;
	if sqlca.sqlcode = 0 then
		lb_parm.AddItem(add_string)
	elseif sqlca.sqlcode < 0 then
		MessageBox("DataBase Error", sqlca.sqlerrtext)
		return sqlca.sqlcode
	else
		exit
	end if
Loop

lb_parm.SetRedraw(true)
Close dyn_cursor;
return 0
end function

